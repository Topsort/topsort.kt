import io.gitlab.arturbosch.detekt.Detekt

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.detekt)
    alias(libs.plugins.vanniktech.publish)
    alias(libs.plugins.bcv)
    alias(libs.plugins.kover)
    alias(libs.plugins.dokka)
}

android {
    namespace 'com.topsort.analytics'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

kotlin {
    jvmToolchain(17)
}

tasks.withType(Detekt).configureEach {
    config = files(["$projectDir/../detekt.yaml"])
}

kover {
    reports {
        verify {
            rule {
                minBound(35)
            }
        }
        filters {
            excludes {
                classes("com.topsort.analytics.BuildConfig")
                classes("com.topsort.analytics.EventPipeline\$EventEmitterWorker")
                packages("com.topsort.analytics.worker")
            }
        }
    }
}

tasks.named('dokkaHtml') {
    outputDirectory = layout.buildDirectory.dir('dokka/html')
    dokkaSourceSets {
        named('main') {
            moduleName = 'Topsort Analytics'
            includeNonPublic = false
            skipEmptyPackages = true
            sourceLink {
                localDirectory = file("src/main/java")
                remoteUrl = new URL("https://github.com/Topsort/topsort.kt/blob/main/TopsortAnalytics/src/main/java")
                remoteLineSuffix = "#L"
            }
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.annotation
    implementation libs.androidx.work.runtime
    implementation libs.androidx.datastore.preferences
    implementation libs.jodatime
    implementation libs.coil

    testImplementation libs.junit4
    testImplementation libs.org.json
    testImplementation libs.assertj.core
    testImplementation libs.mockk
    testImplementation libs.kotlinx.coroutines.test

    androidTestImplementation libs.assertj.core
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.androidx.work.testing
}
